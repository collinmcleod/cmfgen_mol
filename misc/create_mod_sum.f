!
! Simple program to read in a FILE (MOD_DIRS) containg a list of MODEL directories.
! Program reads the MOD_SUM file in each of these directories, and outputs a summary
! file (MODEL_SUMMARY).
!
	PROGRAM CREATE_MOD_SUM
	IMPLICIT NONE
!
	REAL*8 T1,T2,T3
	INTEGER K,IOS
!
	CHARACTER(LEN=200) STRING
	CHARACTER(LEN=100) FILENAME,MOD_FILENAME
	CHARACTER(LEN=11)  LSTAR,MDOT
	CHARACTER(LEN=9)  RSTAR,VINF
	CHARACTER(LEN=8)  TEFF
	CHARACTER(LEN=7)  LOGG
	CHARACTER(LEN=8)  NHeH,XCARB,XNIT,XOXY
	CHARACTER(LEN=7)  NT
	CHARACTER(LEN=6)  BETA
	CHARACTER(LEN=7)  CL_F
	CHARACTER(LEN=8)  CL_VF
	CHARACTER(LEN=11) DATE
!
! Initialize header variables. Done this way as change in string length does
! not require a change here.
!
	LSTAR='L/Lsun'; MDOT='Mdot'; VINF='Vinf'
	RSTAR='R/Lsun'; LOGG='Logg'; NT='NT'
	CL_F='f'; CL_VF='V(f)';  BETA='Beta'
	XCARB='X(C/s)'; XNIT='X(N/s)'; XOXY='X(O/s)'
	NHeH='N(He/H)'; TEFF='Teff'; DATE='Date'
	FILENAME=' '
!
	TEFF=ADJUSTR(TEFF)
	LOGG=ADJUSTR(LOGG)
	LSTAR=ADJUSTR(LSTAR)
	RSTAR=ADJUSTR(RSTAR)
	MDOT=ADJUSTR(MDOT)
	CL_F=ADJUSTR(CL_F)
	CL_VF=ADJUSTR(CL_VF)
	VINF=ADJUSTR(VINF)
	BETA=ADJUSTR(BETA)
	NHeH=ADJUSTR(NHeH)
	XCARB=ADJUSTR(XCARB)
	XNIT=ADJUSTR(XNIT)
	XOXY=ADJUSTR(XOXY)
	NT=ADJUSTR(NT)
	DATE=ADJUSTR(DATE)
!
	OPEN(UNIT=12,FILE='MODEL_SUMMARY',STATUS='UNKNOWN',ACTION='WRITE')
!
! Outut header.
! Note: This write should be identical to that at the end of the program.
!
	WRITE(12,'(A,T40,14A,3X,A11)')TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XNIT,XOXY,NT,DATE

	OPEN(UNIT=8,FILE='MOD_DIRS',STATUS='OLD',ACTION='READ')
	DO WHILE(1 .EQ. 1)
	  READ(8,'(A)',END=1000)FILENAME
!
	  MOD_FILENAME=TRIM(FILENAME)//'/MOD_SUM'
	  OPEN(UNIT=10,FILE=MOD_FILENAME,STATUS='OLD',ACTION='READ')
!
	  STRING=' '
	  DO WHILE( INDEX(STRING,'Finalized') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'on:')+3
	  STRING=ADJUSTL(STRING(K:))
	  DATE=STRING(1:11)
!
	  DO WHILE( INDEX(STRING,'NT[') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'NT[')
	  STRING=STRING(K+3:)
	  K=INDEX(STRING,']')
	  NT=STRING(1:K-1)
	  WRITE(6,*)'NT=',TRIM(NT)
!
	  DO WHILE( INDEX(STRING,'L*') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,' ')
	  READ(STRING(4:K),*)T1
	  WRITE(LSTAR,'(ES8.2)')T1
	  WRITE(6,*)'Done LSTAR=',TRIM(LSTAR)
!
	  STRING=STRING(K:)
	  K=INDEX(STRING,'Mdot')
	  STRING=STRING(K:)
	  READ(STRING(6:14),*)T1
	  WRITE(MDOT,'(ES8.2)')T1
!
	  DO WHILE( INDEX(STRING,'6.667E-01') .EQ. 0 )
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Rsun')
	  STRING=STRING(K+5:)
	  K=INDEX(STRING,' ')
	  RSTAR=STRING(1:K)
!
	  K=INDEX(STRING,'Teff')
	  STRING=STRING(K+8:)
	  READ(STRING,*)T1
	  WRITE(TEFF,'(F5.2)')T1/1.0D+04
!
	  K=INDEX(STRING,'Log g')
	  IF(K .EQ. 0)THEN
	    LOGG=' '
	  ELSE
	    STRING=STRING(K+6:)
	    LOGG=STRING(1:4)
	  END IF
!
	  DO WHILE( INDEX(STRING,'Vinf1') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Vinf1')
	  STRING=STRING(K+6:)
	  K=INDEX(STRING,' ')
	  VINF=STRING(1:K)
	  K=LEN_TRIM(VINF)
	  DO WHILE(VINF(K:K) .EQ. '0' .AND. VINF(K-1:K-1) .NE. '.')
	    VINF(K:K)=' '
	    K=K-1
	  END DO
!
	  DO WHILE( INDEX(STRING,'Beta1') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'Beta1')
	  STRING=STRING(K+6:)
	  K=INDEX(STRING,' ')
	  READ(STRING(1:K),*)T1
	  WRITE(BETA,'(F3.1)')T1
!
	  DO WHILE( INDEX(STRING,'HYD') .EQ. 0)
	    READ(10,'(A)')STRING
	  END DO
	  K=INDEX(STRING,'HYD')+4
	  READ(STRING(K:),*)T1
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'HE')+4
	  READ(STRING(K:),*)T2
	  WRITE(NHeH,'(F5.2)')T2/T1
!
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'CARB')+4
	  READ(STRING(K:),*)T3,T2,T1
	  WRITE(XCARB,'(F5.3)')T1
!	
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'NIT')+4
	  READ(STRING(K:),*)T3,T2,T1
	  WRITE(XNIT,'(F6.3)')T1
!	
	  READ(10,'(A)')STRING
	  K=INDEX(STRING,'OXY')+4
	  READ(STRING(K:),*)T3,T2,T1
	  WRITE(XOXY,'(F5.3)')T1
!
	  IOS=0
	  DO WHILE(INDEX(STRING,'CL_P') .EQ. 0 .AND. IOS .EQ. 0)
	    READ(10,'(A)',IOSTAT=IOS)STRING
	  END DO
	  IF(IOS .EQ. 0)THEN
	    K=INDEX(STRING,'CL_P_1')+7
	    READ(STRING(K:),*)T1
	    WRITE(CL_F,'(F4.2)')T1
	    K=INDEX(STRING,'CL_P_2')+7
	    READ(STRING(K:),*)T1
	    WRITE(CL_VF,'(F6.1)')T1
	  ELSE
	    WRITE(CL_F,'(F4.2)')1.0D0
	    WRITE(CL_VF,'(F6.1)')0.0D0
	  END IF
!	
	  CLOSE(UNIT=10)
	  WRITE(6,*)'Read ',TRIM(FILENAME)
!
	  TEFF=ADJUSTR(TEFF)
	  LOGG=ADJUSTR(LOGG)
	  LSTAR=ADJUSTR(LSTAR)
	  RSTAR=ADJUSTR(RSTAR)
	  MDOT=ADJUSTR(MDOT)
	  CL_F=ADJUSTR(CL_F)
	  CL_VF=ADJUSTR(CL_VF)
	  VINF=ADJUSTR(VINF)
	  BETA=ADJUSTR(BETA)
	  NHeH=ADJUSTR(NHeH)
	  XCARB=ADJUSTR(XCARB)
	  XNIT=ADJUSTR(XNIT)
	  XOXY=ADJUSTR(XOXY)
	  NT=ADJUSTR(NT)
!
	  WRITE(12,'(A,T40,14A,3X,A11)')TRIM(ADJUSTL(FILENAME)),TEFF,LOGG,LSTAR,RSTAR,MDOT,
	1                          CL_F,CL_VF,VINF,BETA,NHeH,XCARB,XNIT,XOXY,NT,DATE
	  FLUSH(UNIT=12)
!
	END DO
1000	CONTINUE
!
	STOP
	END
